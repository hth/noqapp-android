buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}
import groovy.xml.Namespace

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply from: '../config/quality.gradle'

if (!hasProperty('env')) {
    env = System.env['GRADLE_ENV']
}

def buildNumber = System.getenv("BUILD_NUMBER") ?: "0"

def manifestVersionName() {
    def manifestFile = file(project.projectDir.absolutePath + '/src/main/AndroidManifest.xml')
    def ns = new Namespace("http://schemas.android.com/apk/res/android", "android")
    def xml = new XmlParser().parse(manifestFile)
    return xml.attributes()[ns.versionName].toString()
}

android {
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.0'
    }
    archivesBaseName = "merchant"

    compileSdkVersion 27
    buildToolsVersion "28.0.3"

    def versionPropsFile = file('version.properties')
    def versionMajor = 0
    def versionMinor = 0
    def versionPatch = 0

    if (versionPropsFile.canRead()) {
        if (env == "release" || env == "sandbox") {
            def Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionMajor = versionProps['VERSION_MAJOR'].toInteger()
            versionMinor = versionProps['VERSION_MINOR'].toInteger()
            versionPatch = versionProps['VERSION_PATCH'].toInteger()
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    def versionBuildPropsFile = file('version.build.properties')
    def versionBuild = 1

    if (env == "release" || env == "sandbox") {
        if (versionBuildPropsFile.canRead()) {
            def Properties versionBuildProps = new Properties()
            versionBuildProps.load(new FileInputStream(versionBuildPropsFile))

            if (versionBuildProps['VERSION_MAJOR'].toInteger() != versionMajor ||
                    versionBuildProps['VERSION_MINOR'].toInteger() != versionMinor) {

                println "versionPatch: " + 0.toString() +
                        " versionBuild: " + versionBuildProps['VERSION_BUILD']
                versionBuild = versionBuildProps['VERSION_BUILD'].toInteger() + 1

                versionBuildProps['VERSION_MAJOR'] = versionMajor.toString()
                versionBuildProps['VERSION_MINOR'] = versionMinor.toString()
                versionBuildProps['VERSION_PATCH'] = versionPatch.toString()
                versionBuildProps['VERSION_BUILD'] = versionBuild.toString()
                versionBuildProps.store(versionBuildPropsFile.newWriter(), null)
            } else {
                println "versionPatch: " + versionBuildProps['VERSION_PATCH'] +
                        " versionBuild: " + versionBuildProps['VERSION_BUILD']
                versionBuild = versionBuildProps['VERSION_BUILD'].toInteger() + 1
                versionPatch = versionBuildProps['VERSION_PATCH'].toInteger() + 1

                versionBuildProps['VERSION_MAJOR'] = versionMajor.toString()
                versionBuildProps['VERSION_MINOR'] = versionMinor.toString()
                versionBuildProps['VERSION_BUILD'] = versionBuild.toString()
                versionBuildProps['VERSION_PATCH'] = versionPatch.toString()
                versionBuildProps.store(versionBuildPropsFile.newWriter(), null)
            }
        } else {
            throw new GradleException("Could not read version.build.properties!")
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    defaultConfig {
        applicationId "com.noqapp.android.merchant"
        minSdkVersion 19
        targetSdkVersion 27
        multiDexEnabled true
        testHandleProfiling true
        testFunctionalTest true
        versionCode versionBuild
        versionName versionMajor + "." + versionMinor + "." + versionPatch
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // For now we enable strict mode for all the configs
        buildConfigField 'boolean', 'STRICT_MODE', 'true'
        buildConfigField "String", "BUILD", "\"$buildNumber\""

        if (env == 'debug' || env == "sandbox" || env == 'release') {
            apkName = "-" + versionMajor + "." + versionMinor + "." + versionPatch + "." + versionBuild + "-" + buildNumber + ".apk"
        }

        ext.versionName = manifestVersionName()
        println "Building NoQueue NoQApp for " +
                "environment: $env " +
                "app-version: $versionName " +
                "apkName ends with : $apkName"

        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                output.outputFileName = new File(
                        "../",
                        output.outputFile.name.replace(".apk", "$apkName"))
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
    }

    signingConfigs {
        sandbox
        release
    }
    apply from: 'noqapp-signing/signing.gradle'

    buildTypes {
        def BOOLEAN = "boolean"
        def STRING = "String"
        def TRUE = "true"
        def FALSE = "false"
        def LOG_HTTP_REQUESTS = "LOG_HTTP_REQUESTS"
        def REPORT_CRASHES = "REPORT_CRASHES"
        def ENABLE_VIEW_SERVER = "ENABLE_VIEW_SERVER"
        def ENABLE_SHARING = "ENABLE_SHARING"
        def DEBUG_IMAGES = "DEBUG_IMAGES"

        debug {
            debuggable true
            //when minifyEnabled is true gets exception com.android.dx.cf.code.SimException:
            minifyEnabled false

            proguardFile '../library-proguard/proguard-butterknife-7.pro'
            proguardFile '../library-proguard/proguard-crashlytics.pro'
            proguardFile '../library-proguard/proguard-facebook.pro'
            proguardFile '../library-proguard/proguard-facebook-conceal.pro'
            proguardFile '../library-proguard/proguard-google-analytics.pro'
            proguardFile '../library-proguard/proguard-google-play-services.pro'
            proguardFile '../library-proguard/proguard-gson.pro'
            proguardFile '../library-proguard/proguard-jackson-2.pro'
            proguardFile '../library-proguard/proguard-org-apache.pro'
            proguardFile '../library-proguard/proguard-square-okhttp3.pro'
            proguardFile '../library-proguard/proguard-square-retrofit2.pro'

            proguardFile 'merchant-debug-proguard.config'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField BOOLEAN, LOG_HTTP_REQUESTS, TRUE
            buildConfigField BOOLEAN, REPORT_CRASHES, FALSE
            buildConfigField BOOLEAN, ENABLE_VIEW_SERVER, TRUE
            buildConfigField BOOLEAN, ENABLE_SHARING, TRUE
            buildConfigField BOOLEAN, DEBUG_IMAGES, TRUE

            buildConfigField STRING, "NOQAPP_MOBILE", endpointSandbox
            buildConfigField STRING, "PROVIDER_AUTHORITY", providerAuthorityStaging
            buildConfigField STRING, "AWSS3", sandboxEndpointAwsS3
            buildConfigField STRING, "PROFILE_BUCKET", profileImageLocation
            buildConfigField STRING, "SERVICE_BUCKET", serviceImageLocation
        }

        sandbox {
            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            matchingFallbacks = ['debug', 'release']
            proguardFile '../library-proguard/proguard-butterknife-7.pro'
            proguardFile '../library-proguard/proguard-crashlytics.pro'
            proguardFile '../library-proguard/proguard-facebook.pro'
            proguardFile '../library-proguard/proguard-facebook-conceal.pro'
            proguardFile '../library-proguard/proguard-google-analytics.pro'
            proguardFile '../library-proguard/proguard-google-play-services.pro'
            proguardFile '../library-proguard/proguard-gson.pro'
            proguardFile '../library-proguard/proguard-jackson-2.pro'
            proguardFile '../library-proguard/proguard-org-apache.pro'
            proguardFile '../library-proguard/proguard-square-okhttp3.pro'
            proguardFile '../library-proguard/proguard-square-retrofit2.pro'

            proguardFile 'merchant-release-proguard.config'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.sandbox

            buildConfigField BOOLEAN, LOG_HTTP_REQUESTS, FALSE
            buildConfigField BOOLEAN, REPORT_CRASHES, TRUE
            buildConfigField BOOLEAN, ENABLE_VIEW_SERVER, FALSE
            buildConfigField BOOLEAN, ENABLE_SHARING, FALSE
            buildConfigField BOOLEAN, DEBUG_IMAGES, FALSE

            buildConfigField STRING, "NOQAPP_MOBILE", endpointSandbox
            buildConfigField STRING, "PROVIDER_AUTHORITY", providerAuthorityLive
            buildConfigField STRING, "AWSS3", sandboxEndpointAwsS3
            buildConfigField STRING, "PROFILE_BUCKET", profileImageLocation
            buildConfigField STRING, "SERVICE_BUCKET", serviceImageLocation
            buildConfigField STRING, "MEDICAL_FORM_VERSION", medicalFormVersion
        }

        release {
            debuggable false
            minifyEnabled true
            zipAlignEnabled true

            proguardFile '../library-proguard/proguard-butterknife-7.pro'
            proguardFile '../library-proguard/proguard-crashlytics.pro'
            proguardFile '../library-proguard/proguard-facebook.pro'
            proguardFile '../library-proguard/proguard-facebook-conceal.pro'
            proguardFile '../library-proguard/proguard-google-analytics.pro'
            proguardFile '../library-proguard/proguard-google-play-services.pro'
            proguardFile '../library-proguard/proguard-gson.pro'
            proguardFile '../library-proguard/proguard-jackson-2.pro'
            proguardFile '../library-proguard/proguard-org-apache.pro'
            proguardFile '../library-proguard/proguard-square-okhttp3.pro'
            proguardFile '../library-proguard/proguard-square-retrofit2.pro'

            proguardFile 'merchant-release-proguard.config'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            buildConfigField BOOLEAN, LOG_HTTP_REQUESTS, FALSE
            buildConfigField BOOLEAN, REPORT_CRASHES, TRUE
            buildConfigField BOOLEAN, ENABLE_VIEW_SERVER, FALSE
            buildConfigField BOOLEAN, ENABLE_SHARING, FALSE
            buildConfigField BOOLEAN, DEBUG_IMAGES, FALSE

            buildConfigField STRING, "NOQAPP_MOBILE", endpointLive
            buildConfigField STRING, "PROVIDER_AUTHORITY", providerAuthorityLive
            buildConfigField STRING, "AWSS3", releaseEndpointAwsS3
            buildConfigField STRING, "PROFILE_BUCKET", profileImageLocation
            buildConfigField STRING, "SERVICE_BUCKET", serviceImageLocation
            buildConfigField STRING, "MEDICAL_FORM_VERSION", medicalFormVersion
        }
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet true
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings true
        // if true, emit full/absolute paths to files with errors (true by default)
        //absolutePaths true
        // if true, check all issues, including those that are off by default
        checkAllWarnings true
        // if true, treat all warnings as errors
        warningsAsErrors true
        // turn off checking the given issue id's
        disable 'TypographyFractions', 'TypographyQuotes'
        // turn on the given issue id's
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        // check *only* the given issue id's
        check 'NewApi', 'InlinedApi'
        // if true, don't include source code lines in the error output
        noLines true
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true
        // Fallback lint configuration (default severities, etc.)
        lintConfig file("default-lint.xml")
        // if true, generate a text report of issues (false by default)
        textReport true
        // location to write the output; can be a file or 'stdout'
        textOutput 'stdout'
        // if true, generate an XML report for use by for example Jenkins
        xmlReport false
        // file to write report to (if not specified, defaults to lint-results.xml)
        xmlOutput file("lint-report.xml")
        // if true, generate an HTML report (with issue explanations, sourcecode, etc)
        htmlReport true
        // optional path to report (default will be lint-results.html in the builddir)
        htmlOutput file("lint-report.html")

        // set to true to have all release builds run lint on issues with severity=fatal
        // and abort the build (controlled by abortOnError above) if fatal issues are found
        checkReleaseBuilds true
        // Set the severity of the given issues to fatal (which means they will be
        // checked during release builds (even if the lint target is not included)
        fatal 'NewApi', 'InlineApi'
        // Set the severity of the given issues to error
        error 'Wakelock', 'TextViewEdits'
        // Set the severity of the given issues to warning
        warning 'ResourceAsColor'
        // Set the severity of the given issues to ignore (same as disabling the check)
        ignore 'TypographyQuotes'
    }

    sourceSets {
        androidTest {
            java.srcDirs = ['androidTest/java']
        }
    }


    flavorDimensions "version"

    productFlavors {
        healthcare {
            //select the dimension of flavor
            dimension "version"

            //configure applicationId for app published to Play store
            applicationId "com.noqapp.android.merchant.healthcare"

            //Configure this flavor specific app name published in Play Store
            resValue "string", "flavored_app_name", "Merchant HealthCare"

            buildConfigField "String", "APP_FLAVOR", "\"NQMH\""
        }

        queue {
            dimension "version"

            applicationId "com.noqapp.android.merchant"
            resValue "string", "flavored_app_name", "Merchant Store"

            buildConfigField "String", "APP_FLAVOR", "\"NQMS\""
        }

        tv {
            dimension "version"

            applicationId "com.noqapp.android.merchant.tv"
            resValue "string", "flavored_app_name", "Merchant TV"

            buildConfigField "String", "APP_FLAVOR", "\"NQMT\""
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:mediarouter-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.5.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'net.danlew:android.joda:2.9.9.3'
    implementation 'com.google.guava:guava:23.6.1-android'
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-auth:16.1.0'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') {
        transitive = true
    }
    implementation('com.crashlytics.sdk.android:answers:1.4.1@aar') {
        transitive = true
    }
    implementation 'com.airbnb.android:lottie:2.7.0'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.google.code.gson:gson:2.8.5'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.mockito:mockito-core:2.23.0'
    androidTestImplementation 'org.mockito:mockito-android:2.23.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0-alpha'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'de.hdodenhof:circleimageview:3.0.0'
    implementation 'org.rauschig:jarchivelib:0.8.0'
    implementation 'org.apache.commons:commons-compress:1.18'
    implementation 'com.github.RobertApikyan:SegmentedControl:1.0.9'
    implementation 'com.google.android.gms:play-services-cast-framework:16.1.2'
    implementation 'info.hoang8f:android-segmented:1.0.6'
    implementation 'org.adw.library:discrete-seekbar:1.0.1'
    implementation 'com.nhaarman.supertooltips:library:3.0.0'
    implementation project(':common')
}

apply plugin: 'com.google.gms.google-services'

/** Gradle default task. */
defaultTasks 'clean', 'assembleDebug'
